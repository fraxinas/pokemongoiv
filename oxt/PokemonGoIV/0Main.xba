<?xml version="1.0" encoding="UTF-8"?>
<!DOCTYPE script:module PUBLIC "-//OpenOffice.org//DTD OfficeDocument 1.0//EN" "module.dtd">
<script:module xmlns:script="http://openoffice.org/2000/script" script:name="0Main" script:language="StarBasic">&apos; Copyright (c) 2016 imacat.
&apos; 
&apos; Licensed under the Apache License, Version 2.0 (the &quot;License&quot;);
&apos; you may not use this file except in compliance with the License.
&apos; You may obtain a copy of the License at
&apos; 
&apos;     http://www.apache.org/licenses/LICENSE-2.0
&apos; 
&apos; Unless required by applicable law or agreed to in writing, software
&apos; distributed under the License is distributed on an &quot;AS IS&quot; BASIS,
&apos; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
&apos; See the License for the specific language governing permissions and
&apos; limitations under the License.

&apos; 0Main: The main module for the Pokémon GO IV calculator
&apos;   by imacat &lt;imacat@mail.imacat.idv.tw&gt;, 2016-11-27

Option Explicit

&apos; The base stats of a Pokémon.
Type aStats
	sNo As String
	sPokemon As String
	nStamina As Integer
	nAttack As Integer
	nDefense As Integer
	mEvolved () As String
End Type

&apos; The individual values of a Pokémon.
Type aIV
	fLevel As Double
	nStamina As Integer
	nAttack As Integer
	nDefense As Integer
	nTotal As Integer
	nMaxCP As Integer
	maEvolved () As aEvolvedStats
End Type

&apos; The calculated evolved stats of a Pokémon.
Type aEvolvedStats
	nCP As Integer
	nMaxCP As Integer
End Type

&apos; The parameters to find the individual values.
Type aFindIVParam
	sPokemon As String
	nCP As Integer
	nHP As Integer
	nStarDust As Integer
	nPlayerLevel As Integer
	bIsNew As Boolean
	nTotal As Integer
	sBest As String
	nMax As Integer
	bIsCancelled As Boolean
End Type

Private maBaseStats () As New aStats
Private mCPM () As Double, mStarDust () As Integer

&apos; subMain: The main program
Sub subMain
	Dim aBaseStats As New aStats, maIVs As Variant, nI As Integer
	Dim aQuery As New aFindIVParam
	
	aQuery = fnAskParam
	If aQuery.bIsCancelled Then
		Exit Sub
	End If
	aBaseStats = fnGetBaseStats (aQuery.sPokemon)
	maIVs = fnFindIV (aBaseStats, aQuery)
	If UBound (maIVs) = -1 Then
		MsgBox fnGetResString (&quot;ErrorNotFound&quot;)
	Else
		subSaveIV (aBaseStats, aQuery, maIVs)
	End If
End Sub

&apos; fnAskParam: Asks the users for the parameters for the Pokémon.
Function fnAskParam As aFindIVParam
	Dim oDialog As Object, oDialogModel As Object
	Dim bIsBestAttack As Boolean, bIsBestDefense As Boolean
	Dim bIsBestHP As Boolean
	Dim aQuery As New aFindIVParam
	
	DialogLibraries.loadLibrary &quot;PokemonGoIV&quot;
	oDialog = CreateUnoDialog (DialogLibraries.PokemonGoIV.DlgMain)
	oDialog.getControl (&quot;lstTotal&quot;).setVisible (False)
	oDialog.getControl (&quot;txtBestBefore&quot;).setVisible (False)
	oDialog.getControl (&quot;lstBest&quot;).setVisible (False)
	oDialog.getControl (&quot;txtBestAfter&quot;).setVisible (False)
	oDialog.getControl (&quot;cbxBest2&quot;).setVisible (False)
	oDialog.getControl (&quot;cbxBest3&quot;).setVisible (False)
	oDialog.getControl (&quot;lstMax&quot;).setVisible (False)
	
	oDialog.getControl (&quot;imgPokemon&quot;).getModel.setPropertyValue ( _
		&quot;ImageURL&quot;, fnGetImageUrl (&quot;Unknown&quot;))
	oDialog.getControl (&quot;imgTeamLogo&quot;).getModel.setPropertyValue ( _
		&quot;ImageURL&quot;, fnGetImageUrl (&quot;Unknown&quot;))
	
	If oDialog.execute = 0 Then
		aQuery.bIsCancelled = True
		fnAskParam = aQuery
		Exit Function
	End If
	
	With aQuery
		.sPokemon = oDialog.getControl (&quot;lstPokemon&quot;).getSelectedItem
		.nCP = oDialog.getControl (&quot;numCP&quot;).getValue
		.nHP = oDialog.getControl (&quot;numHP&quot;).getValue
		.nStarDust = CInt (oDialog.getControl (&quot;lstStarDust&quot;).getSelectedItem)
		.nPlayerLevel = CInt (oDialog.getControl (&quot;lstPlayerLevel&quot;).getSelectedItem)
		.nTotal = oDialog.getControl (&quot;lstTotal&quot;).getSelectedItemPos + 1
		.nMax = oDialog.getControl (&quot;lstMax&quot;).getSelectedItemPos + 1
		.bIsCancelled = False
	End With
	If oDialog.getControl (&quot;cbxIsNew&quot;).getState = 1 Then
		aQuery.bIsNew = True
	Else
		aQuery.bIsNew = False
	End If
	
	&apos; The best stats
	bIsBestAttack = False
	bIsBestDefense = False
	bIsBestHP = False
	If oDialog.getControl (&quot;lstBest&quot;).getSelectedItem = &quot;Attack&quot; Then
		bIsBestAttack = True
		If oDialog.getControl (&quot;cbxBest2&quot;).getState = 1 Then
			bIsBestDefense = True
		End If
		If oDialog.getControl (&quot;cbxBest3&quot;).getState = 1 Then
			bIsBestHP = True
		End If
	End If
	If oDialog.getControl (&quot;lstBest&quot;).getSelectedItem = &quot;Defense&quot; Then
		bIsBestDefense = True
		If oDialog.getControl (&quot;cbxBest2&quot;).getState = 1 Then
			bIsBestAttack = True
		End If
		If oDialog.getControl (&quot;cbxBest3&quot;).getState = 1 Then
			bIsBestHP = True
		End If
	End If
	If oDialog.getControl (&quot;lstBest&quot;).getSelectedItem = &quot;HP&quot; Then
		bIsBestHP = True
		If oDialog.getControl (&quot;cbxBest2&quot;).getState = 1 Then
			bIsBestAttack = True
		End If
		If oDialog.getControl (&quot;cbxBest3&quot;).getState = 1 Then
			bIsBestDefense = True
		End If
	End If
	aQuery.sBest = &quot;&quot;
	If bIsBestAttack Then
		aQuery.sBest = aQuery.sBest &amp; &quot;Atk &quot;
	End If
	If bIsBestDefense Then
		aQuery.sBest = aQuery.sBest &amp; &quot;Def &quot;
	End If
	If bIsBestHP Then
		aQuery.sBest = aQuery.sBest &amp; &quot;Sta &quot;
	End If
	
	fnAskParam = aQuery
End Function

&apos; subBtnOKCheck: Checks whether the required columns are filled.
Sub subBtnOKCheck (oEvent As object)
	Dim oDialog As Object
	Dim oPokemon As Object, oCP As Object
	Dim oHP As Object, oStarDust As Object, oOK As Object
	
	oDialog = oEvent.Source.getContext
	oPokemon = oDialog.getControl (&quot;lstPokemon&quot;)
	oCP = oDialog.getControl (&quot;numCP&quot;)
	oHP = oDialog.getControl (&quot;numHP&quot;)
	oStarDust = oDialog.getControl (&quot;lstStarDust&quot;)
	oOK = oDialog.getControl (&quot;btnOK&quot;)
	
	If oPokemon.getSelectedItemPos &lt;&gt; -1 _
			And oCP.getText &lt;&gt; &quot;&quot; _
			And oHP.getText &lt;&gt; &quot;&quot; _
			And oStarDust.getSelectedItemPos &lt;&gt; -1 Then
		oOK.setEnable (True)
	Else
		oOK.setEnable (False)
	End If
End Sub

&apos; subLstPokemonSelected: When the Pokémon is selected.
Sub subLstPokemonSelected (oEvent As object)
	Dim oDialog As Object, sPokemon As String
	Dim oImageModel As Object, sImageId As String
	
	oDialog = oEvent.Source.getContext
	
	&apos; Updates the Pokémon image.
	sPokemon = oDialog.getControl (&quot;lstPokemon&quot;).getSelectedItem
	sImageId = &quot;&quot;
	If sPokemon = &quot;Farfetch&apos;d&quot; Then
		sImageId = &quot;PokemonFarfetchd&quot;
	End If
	If sPokemon = &quot;Nidoran♀&quot; Then
		sImageId = &quot;PokemonNidoranFemale&quot;
	End If
	If sPokemon = &quot;Nidoran♂&quot; Then
		sImageId = &quot;PokemonNidoranMale&quot;
	End If
	If sPokemon = &quot;Mr. Mime&quot; Then
		sImageId = &quot;PokemonMrMime&quot;
	End If
	If sImageId = &quot;&quot; Then
		sImageId = &quot;Pokemon&quot; &amp; sPokemon
	End If
	oImageModel = oDialog.getControl (&quot;imgPokemon&quot;).getModel
	oImageModel.setPropertyValue (&quot;ImageURL&quot;, _
		fnGetImageUrl (sImageId))
	
	&apos; Updates the text of the stats total appraisal.
	subUpdateTotalAppraisal (oDialog, True)
	&apos; Checks if the required columns are filled.
	subBtnOKCheck (oEvent)
End Sub

&apos; subRdoTeamValorItemChanged: When Team Valor is selected.
Sub subRdoTeamValorItemChanged (oEvent As object)
	Dim oDialog As Object, oList As Object, oText As Object
	Dim oImageModel As Object
	Dim mItems () As String
	
	oDialog = oEvent.Source.getContext
	
	oImageModel = oDialog.getControl (&quot;imgTeamLogo&quot;).getModel
	oImageModel.setPropertyValue (&quot;ImageURL&quot;, _
		fnGetImageUrl (&quot;TeamLogoValor&quot;))
	oImageModel = oDialog.getControl (&quot;imgTeamLeader&quot;).getModel
	oImageModel.setPropertyValue (&quot;ImageURL&quot;, _
		fnGetImageUrl (&quot;TeamLeaderCandela&quot;))
	
	oText = oDialog.getControl (&quot;txtLeaderAppraise&quot;)
	oText.setVisible (True)
	oText.setText (fnGetResString (&quot;AppraiseFromCandela&quot;))
	
	&apos; Updates the text of the stats total appraisal.
	subUpdateTotalAppraisal (oDialog, False)
	
	&apos; Updates the text of the best stat appraisal.
	subUpdateBestStatAppraisal (oDialog, _
		&quot;Its&quot;, 8, &quot;is its strongest feature.&quot;, 65)
	
	mItems = Array ( _
		&quot;I&apos;m blown away by its stats. WOW!&quot;, _
		&quot;It&apos;s got excellent stats! How exciting!&quot;, _
		&quot;Its stats indicate that in battle, it&apos;ll get the job done.&quot;, _
		&quot;Its stats don&apos;t point to greatness in battle.&quot;)
	oList = oDialog.getControl (&quot;lstMax&quot;)
	oList.removeItems (0, oList.getItemCount())
	oList.addItems (mItems, 0)
	oList.setVisible (True)
End Sub

&apos; subRdoTeamMysticItemChanged: When Team Mystic is selected.
Sub subRdoTeamMysticItemChanged (oEvent As object)
	Dim oDialog As Object, oList As Object, oText As Object
	Dim oImageModel As Object
	Dim mItems () As String
	
	oDialog = oEvent.Source.getContext
	
	oImageModel = oDialog.getControl (&quot;imgTeamLogo&quot;).getModel
	oImageModel.setPropertyValue (&quot;ImageURL&quot;, _
		fnGetImageUrl (&quot;TeamLogoMystic&quot;))
	oImageModel = oDialog.getControl (&quot;imgTeamLeader&quot;).getModel
	oImageModel.setPropertyValue (&quot;ImageURL&quot;, _
		fnGetImageUrl (&quot;TeamLeaderBlanche&quot;))
	
	oText = oDialog.getControl (&quot;txtLeaderAppraise&quot;)
	oText.setVisible (True)
	oText.setText (fnGetResString (&quot;AppraiseFromBlanche&quot;))
	
	&apos; Updates the text of the stats total appraisal.
	subUpdateTotalAppraisal (oDialog, False)
	
	&apos; Updates the text of the best stat appraisal.
	subUpdateBestStatAppraisal (oDialog, _
		&quot;I see that its best attribute is its&quot;, 85, &quot;.&quot;, 5)
	
	mItems = Array ( _
		&quot;Its stats exceed my calculations. It&apos;s incredible!&quot;, _
		&quot;I am certainly impressed by its stats, I must say.&quot;, _
		&quot;Its stats are noticeably trending to the positive.&quot;, _
		&quot;Its stats are not out of the norm, in my opinion.&quot;)
	oList = oDialog.getControl (&quot;lstMax&quot;)
	oList.removeItems (0, oList.getItemCount())
	oList.addItems (mItems, 0)
	oList.setVisible (True)
End Sub

&apos; subRdoTeamInstinctItemChanged: When Team Instinct is selected.
Sub subRdoTeamInstinctItemChanged (oEvent As object)
	Dim oDialog As Object, oList As Object, oText As Object
	Dim oImageModel As Object
	Dim mItems () As String
	
	oDialog = oEvent.Source.getContext
	
	oImageModel = oDialog.getControl (&quot;imgTeamLogo&quot;).getModel
	oImageModel.setPropertyValue (&quot;ImageURL&quot;, _
		fnGetImageUrl (&quot;TeamLogoInstinct&quot;))
	oImageModel = oDialog.getControl (&quot;imgTeamLeader&quot;).getModel
	oImageModel.setPropertyValue (&quot;ImageURL&quot;, _
		fnGetImageUrl (&quot;TeamLeaderSpark&quot;))
	
	oText = oDialog.getControl (&quot;txtLeaderAppraise&quot;)
	oText.setVisible (True)
	oText.setText (fnGetResString (&quot;AppraiseFromSpark&quot;))
	
	&apos; Updates the text of the stats total appraisal.
	subUpdateTotalAppraisal (oDialog, False)
	
	&apos; Updates the text of the best stat appraisal.
	subUpdateBestStatAppraisal (oDialog, _
		&quot;Its best quality is&quot;, 45, &quot;.&quot;, 5)
	
	mItems = Array ( _
		&quot;Its stats are the best I&apos;ve ever seen! No doubt about it!&quot;, _
		&quot;Its stats are really strong! Impressive.&quot;, _
		&quot;It&apos;s definitely got some good stats. Definitely!&quot;, _
		&quot;Its stats are all right, but kinda basic, as far as I can see.&quot;)
	oList = oDialog.getControl (&quot;lstMax&quot;)
	oList.removeItems (0, oList.getItemCount())
	oList.addItems (mItems, 0)
	oList.setVisible (True)
End Sub

&apos; subUpdateBestStatAppraisal: Updates the text of the best stat appraisal.
Sub subUpdateBestStatAppraisal (oDialog As Object, _
		sBefore As String, nBeforeWidth As Integer, _
		sAfter As String, nAfterWidth As Integer)
	Dim oText As Object, oList As Object, nX As Integer
	Dim mItems () As String
	
	oText = oDialog.getControl (&quot;txtBestBefore&quot;)
	oText.getModel.setPropertyValue (&quot;Width&quot;, nBeforeWidth)
	oText.setVisible (True)
	oText.setText (sBefore)
	nX = oText.getModel.getPropertyValue (&quot;PositionX&quot;) + nBeforeWidth
	
	mItems = Array (&quot;Attack&quot;, &quot;Defense&quot;, &quot;HP&quot;)
	oList = oDialog.getControl (&quot;lstBest&quot;)
	oList.removeItems (0, oList.getItemCount())
	oList.addItems (mItems, 0)
	oList.getModel.setPropertyValue (&quot;PositionX&quot;, nX)
	oList.setVisible (True)
	nX = nX + oList.getModel.getPropertyValue (&quot;Width&quot;) + 2
	
	oText = oDialog.getControl (&quot;txtBestAfter&quot;)
	oText.getModel.setPropertyValue (&quot;PositionX&quot;, nX)
	oText.getModel.setPropertyValue (&quot;Width&quot;, nAfterWidth)
	oText.setVisible (True)
	oText.setText (sAfter)
	
	oList = oDialog.getControl (&quot;cbxBest2&quot;)
	oList.setVisible (False)
	
	oList = oDialog.getControl (&quot;cbxBest3&quot;)
	oList.setVisible (False)
End Sub

&apos; subLstBestItemChanged: When the best stat is selected.
Sub subLstBestItemChanged (oEvent As object)
	Dim oDialog As Object, oCheckBox As Object
	
	oDialog = oEvent.Source.getContext
	If oDialog.getControl (&quot;rdoTeamValor&quot;).getState Then
		If oDialog.getControl (&quot;lstBest&quot;).getSelectedItem = &quot;Attack&quot; Then
			oCheckBox = oDialog.getControl (&quot;cbxBest2&quot;)
			oCheckBox.setLabel (&quot;I&apos;m just as impressed with its Defense.&quot;)
			oCheckBox.setVisible (True)
			oCheckBox.setState (0)
			oCheckBox = oDialog.getControl (&quot;cbxBest3&quot;)
			oCheckBox.setLabel (&quot;I&apos;m just as impressed with its HP.&quot;)
			oCheckBox.setVisible (True)
			oCheckBox.setState (0)
		End If
		If oDialog.getControl (&quot;lstBest&quot;).getSelectedItem = &quot;Defense&quot; Then
			oCheckBox = oDialog.getControl (&quot;cbxBest2&quot;)
			oCheckBox.setLabel (&quot;I&apos;m just as impressed with its Attack.&quot;)
			oCheckBox.setVisible (True)
			oCheckBox.setState (0)
			oCheckBox = oDialog.getControl (&quot;cbxBest3&quot;)
			oCheckBox.setLabel (&quot;I&apos;m just as impressed with its HP.&quot;)
			oCheckBox.setVisible (True)
			oCheckBox.setState (0)
		End If
		If oDialog.getControl (&quot;lstBest&quot;).getSelectedItem = &quot;HP&quot; Then
			oCheckBox = oDialog.getControl (&quot;cbxBest2&quot;)
			oCheckBox.setLabel (&quot;I&apos;m just as impressed with its Attack.&quot;)
			oCheckBox.setVisible (True)
			oCheckBox.setState (0)
			oCheckBox = oDialog.getControl (&quot;cbxBest3&quot;)
			oCheckBox.setLabel (&quot;I&apos;m just as impressed with its Defense.&quot;)
			oCheckBox.setVisible (True)
			oCheckBox.setState (0)
		End If
	End If
	If oDialog.getControl (&quot;rdoTeamMystic&quot;).getState Then
		If oDialog.getControl (&quot;lstBest&quot;).getSelectedItem = &quot;Attack&quot; Then
			oCheckBox = oDialog.getControl (&quot;cbxBest2&quot;)
			oCheckBox.setLabel (&quot;It is matched equally by its Defense.&quot;)
			oCheckBox.setVisible (True)
			oCheckBox.setState (0)
			oCheckBox = oDialog.getControl (&quot;cbxBest3&quot;)
			oCheckBox.setLabel (&quot;It is matched equally by its HP.&quot;)
			oCheckBox.setVisible (True)
			oCheckBox.setState (0)
		End If
		If oDialog.getControl (&quot;lstBest&quot;).getSelectedItem = &quot;Defense&quot; Then
			oCheckBox = oDialog.getControl (&quot;cbxBest2&quot;)
			oCheckBox.setLabel (&quot;It is matched equally by its Attack.&quot;)
			oCheckBox.setVisible (True)
			oCheckBox.setState (0)
			oCheckBox = oDialog.getControl (&quot;cbxBest3&quot;)
			oCheckBox.setLabel (&quot;It is matched equally by its HP.&quot;)
			oCheckBox.setVisible (True)
			oCheckBox.setState (0)
		End If
		If oDialog.getControl (&quot;lstBest&quot;).getSelectedItem = &quot;HP&quot; Then
			oCheckBox = oDialog.getControl (&quot;cbxBest2&quot;)
			oCheckBox.setLabel (&quot;It is matched equally by its Attack.&quot;)
			oCheckBox.setVisible (True)
			oCheckBox.setState (0)
			oCheckBox = oDialog.getControl (&quot;cbxBest3&quot;)
			oCheckBox.setLabel (&quot;It is matched equally by its Defense.&quot;)
			oCheckBox.setVisible (True)
			oCheckBox.setState (0)
		End If
	End If
	If oDialog.getControl (&quot;rdoTeamInstinct&quot;).getState Then
		If oDialog.getControl (&quot;lstBest&quot;).getSelectedItem = &quot;Attack&quot; Then
			oCheckBox = oDialog.getControl (&quot;cbxBest2&quot;)
			oCheckBox.setLabel (&quot;Its Defense is great, too!&quot;)
			oCheckBox.setVisible (True)
			oCheckBox = oDialog.getControl (&quot;cbxBest3&quot;)
			oCheckBox.setLabel (&quot;Its HP is great, too!&quot;)
			oCheckBox.setVisible (True)
		End If
		If oDialog.getControl (&quot;lstBest&quot;).getSelectedItem = &quot;Defense&quot; Then
			oCheckBox = oDialog.getControl (&quot;cbxBest2&quot;)
			oCheckBox.setLabel (&quot;Its Attack is great, too!&quot;)
			oCheckBox.setVisible (True)
			oCheckBox.setState (0)
			oCheckBox = oDialog.getControl (&quot;cbxBest3&quot;)
			oCheckBox.setLabel (&quot;Its HP is great, too!&quot;)
			oCheckBox.setVisible (True)
			oCheckBox.setState (0)
		End If
		If oDialog.getControl (&quot;lstBest&quot;).getSelectedItem = &quot;HP&quot; Then
			oCheckBox = oDialog.getControl (&quot;cbxBest2&quot;)
			oCheckBox.setLabel (&quot;Its Attack is great, too!&quot;)
			oCheckBox.setVisible (True)
			oCheckBox.setState (0)
			oCheckBox = oDialog.getControl (&quot;cbxBest3&quot;)
			oCheckBox.setLabel (&quot;Its Defense is great, too!&quot;)
			oCheckBox.setVisible (True)
			oCheckBox.setState (0)
		End If
	End If
End Sub

&apos; subUpdateTotalAppraisal: Updates the text of the stats total
&apos;						   appraisal.
Sub subUpdateTotalAppraisal ( _
		oDialog As Object, bIsKeepSelected As Boolean)
	Dim sPokemon As String, oList As Object, nSelected As Integer
	Dim mItems () As String, nI As Integer
	
	If oDialog.getControl (&quot;rdoTeamValor&quot;).getState Then
		mItems = Array ( _
			&quot;Overall, your [Pokémon] simply amazes me. It can accomplish anything!&quot;, _
			&quot;Overall, your [Pokémon] is a strong Pokémon. You should be proud!&quot;, _
			&quot;Overall, your [Pokémon] is a decent Pokémon.&quot;, _
			&quot;Overall, your [Pokémon] may not be great in battle, but I still like it!&quot;)
	End If
	If oDialog.getControl (&quot;rdoTeamMystic&quot;).getState Then
		mItems = Array ( _
			&quot;Overall, your [Pokémon] is a wonder! What a breathtaking Pokémon!&quot;, _
			&quot;Overall, your [Pokémon] has certainly caught my attention.&quot;, _
			&quot;Overall, your [Pokémon] is above average.&quot;, _
			&quot;Overall, your [Pokémon] is not likely to make much headway in battle.&quot;)
	End If
	If oDialog.getControl (&quot;rdoTeamInstinct&quot;).getState Then
		mItems = Array ( _
			&quot;Overall, your [Pokémon] looks like it can really battle with the best of them!&quot;, _
			&quot;Overall, your [Pokémon] is really strong!&quot;, _
			&quot;Overall, your [Pokémon] is pretty decent!&quot;, _
			&quot;Overall, your [Pokémon] has room for improvement as far as battling goes.&quot;)
	End If
	&apos; The team was not selected yet.
	If UBound (mItems) = -1 Then
		Exit sub
	End If
	
	sPokemon = oDialog.getControl (&quot;lstPokemon&quot;).getSelectedItem
	If sPokemon &lt;&gt; &quot;&quot; Then
		For nI = 0 To UBound (mItems)
			mItems (nI) = fnReplace (mItems (nI), _
				&quot;[Pokémon]&quot;, sPokemon)
		Next nI
	End If
	
	oList = oDialog.getControl (&quot;lstTotal&quot;)
	If bIsKeepSelected Then
		nSelected = oList.getSelectedItemPos
	End If
	oList.removeItems (0, oList.getItemCount())
	oList.addItems (mItems, 0)
	If bIsKeepSelected Then
		oList.selectItemPos (nSelected, True)
	End If
	oList.setVisible (True)
End Sub

&apos; fnFindIV: Finds the possible individual values of the Pokémon
Function fnFindIV ( _
		aBaseStats As aStats, aQuery As aFindIVParam) As Variant
	Dim nEvolved As Integer
	Dim maEvBaseStats () As New aStats, aTempStats As New aStats
	Dim maIV () As New aIV, aTempIV As New aIV
	Dim fLevel As Double, nStamina As Integer
	Dim nAttack As Integer, nDefense As integer
	Dim nI As Integer, nJ As Integer
	Dim fStep As Double, nN As Integer
	
	If aQuery.sPokemon = &quot;&quot; Then
		fnFindIV = maIV
		Exit Function
	End If
	If aQuery.bIsNew Then
		fStep = 1
	Else
		fStep = 0.5
	End If
	subReadStarDust
	nEvolved = UBound (aBaseStats.mEvolved)
	ReDim Preserve maEvBaseStats (nEvolved) As New aStats
	For nI = 0 To nEvolved
		aTempStats = fnGetBaseStats (aBaseStats.mEvolved (nI))
		With maEvBaseStats (nI)
			.nAttack = aTempStats.nAttack
			.nDefense = aTempStats.nDefense
			.nStamina = aTempStats.nStamina
		End With
	Next nI
	nN = -1
	For fLevel = 1 To UBound (mStarDust) Step fStep
		If mStarDust (CInt (fLevel - 0.5)) = aQuery.nStarDust Then
			For nStamina = 0 To 15
				If fnCalcHP (aBaseStats, fLevel, nStamina) = aQuery.nHP Then
					For nAttack = 0 To 15
						For nDefense = 0 To 15
							If fnCalcCP (aBaseStats, fLevel, nAttack, nDefense, nStamina) = aQuery.nCP _
									And Not fnFilterAppraisals (aQuery, nAttack, nDefense, nStamina) Then
								nN = nN + 1
								ReDim Preserve maIV (nN) As New aIV
								With maIV (nN)
									.fLevel = fLevel
									.nAttack = nAttack
									.nDefense = nDefense
									.nStamina = nStamina
									.nTotal = nAttack _
										+ nDefense + nStamina
								End With
								If aQuery.nPlayerLevel &lt;&gt; 0 Then
									maIV (nN).nMaxCP = fnCalcCP ( _
										aBaseStats, _
										aQuery.nPlayerLevel + 1.5, _
										nAttack, nDefense, nStamina)
								End If
								maIV (nN).maEvolved _
									= fnGetEvolvedArray (nEvolved)
								For nI = 0 To nEvolved
									maIV (nN).maEvolved (nI).nCP _
										= fnCalcCP ( _
											maEvBaseStats (nI), _
											fLevel, nAttack, _
											nDefense, nStamina)
									If aQuery.nPlayerLevel &lt;&gt; 0 Then
										maIV (nN).maEvolved (nI).nMaxCP = fnCalcCP (maEvBaseStats (nI), aQuery.nPlayerLevel + 1.5, nAttack, nDefense, nStamina)
									End If
								Next nI
							End If
						Next nDefense
					Next nAttack
				End If
			Next nStamina
		End If
	Next fLevel
	&apos; Sorts the IVs
	For nI = 0 To UBound (maIV) - 1
		For nJ = nI + 1 To UBound (maIV)
			If fnCompareIV (maIV (nI), maIV (nJ)) &gt; 0 Then
				&apos; This is an array of data.  The data are actually
				&apos; allocated in sequences.  maIV (nI) is not a
				&apos; reference.  They cannot simply be assigned.
				subCopyIV (maIV (nI), aTempIV)
				subCopyIV (maIV (nJ), maIV (nI))
				subCopyIV (aTempIV, maIV (nJ))
			End If
		Next nJ
	Next nI
	fnFindIV = maIV
End Function

&apos; fnCompareIV: Compare two IVs for sorting
Function fnCompareIV (aIVa As aIV, aIVb As aIV) As Double
	Dim nCPa As Integer, nCPb As Integer, nI As Integer
	
	nCPa = aIVa.nMaxCP
	For nI = 0 To UBound (aIVa.maEvolved)
		If nCPa &lt; aIVa.maEvolved (nI).nMaxCP Then
			nCPa = aIVa.maEvolved (nI).nMaxCP
		End If
	Next nI
	nCPb = aIVb.nMaxCP
	For nI = 0 To UBound (aIVb.maEvolved)
		If nCPb &lt; aIVb.maEvolved (nI).nMaxCP Then
			nCPb = aIVb.maEvolved (nI).nMaxCP
		End If
	Next nI
	fnCompareIV = nCPb - nCPa
	If fnCompareIV &lt;&gt; 0 Then
		Exit Function
	End If
	
	nCPa = 0
	For nI = 0 To UBound (aIVa.maEvolved)
		If nCPa &lt; aIVa.maEvolved (nI).nCP Then
			nCPa = aIVa.maEvolved (nI).nCP
		End If
	Next nI
	nCPb = 0
	For nI = 0 To UBound (aIVb.maEvolved)
		If nCPb &lt; aIVb.maEvolved (nI).nCP Then
			nCPb = aIVb.maEvolved (nI).nCP
		End If
	Next nI
	fnCompareIV = nCPb - nCPa
	If fnCompareIV &lt;&gt; 0 Then
		Exit Function
	End If
	
	fnCompareIV = aIVb.nTotal - aIVa.nTotal
	If fnCompareIV &lt;&gt; 0 Then
		Exit Function
	End If
	fnCompareIV = aIVb.fLevel - aIVa.fLevel
	If fnCompareIV &lt;&gt; 0 Then
		Exit Function
	End If
	fnCompareIV = aIVb.nStamina - aIVa.nStamina
	If fnCompareIV &lt;&gt; 0 Then
		Exit Function
	End If
	fnCompareIV = aIVb.nAttack - aIVa.nAttack
	If fnCompareIV &lt;&gt; 0 Then
		Exit Function
	End If
	fnCompareIV = aIVb.nDefense - aIVa.nDefense
	If fnCompareIV &lt;&gt; 0 Then
		Exit Function
	End If
End Function

&apos; subCopyIV: Copies one IV to another
Function subCopyIV (aFrom As aIV, aTo As aIV) As Double
	Dim nI As Integer
	
	With aTo
		.nAttack = aFrom.nAttack
		.nDefense = aFrom.nDefense
		.nStamina = aFrom.nStamina
		.nTotal = aFrom.nTotal
		.nMaxCP = aFrom.nMaxCP
	End With
	aTo.maEvolved = fnGetEvolvedArray (UBound (aFrom.maEvolved))
	For nI = 0 To UBound (aFrom.maEvolved)
		With aTo.maEvolved (nI)
			.nCP = aFrom.maEvolved (nI).nCP
			.nMaxCP = aFrom.maEvolved (nI).nMaxCP
		End With
	Next nI
End Function

&apos; subSaveIV: Saves the found IV
Sub subSaveIV ( _
		aBaseStats As aStats, aQuery As aFindIVParam, maIVs () As aIV)
	Dim oDoc As Object, oSheet As Object
	Dim oRange As Object, oColumns As Object, oRows As Object
	Dim nI As Integer, nJ As Integer, nFront As Integer
	Dim nEvolved As Integer
	Dim mData (Ubound (maIVs) + 1) As Variant, mRow () As Variant
	Dim mProps () As New com.sun.star.beans.PropertyValue
	
	oDoc = StarDesktop.loadComponentFromURL ( _
		&quot;private:factory/scalc&quot;, &quot;_default&quot;, 0, mProps)
	oSheet = oDoc.getSheets.getByIndex (0)
	nEvolved = UBound (maIVs (0).maEvolved) + 1
	
	mRow = Array ( _
		&quot;No&quot;, &quot;Pokemon&quot;, &quot;CP&quot;, &quot;HP&quot;, &quot;Star dust&quot;, _
		&quot;Lv&quot;, &quot;Atk&quot;, &quot;Def&quot;, &quot;Sta&quot;, &quot;IV&quot;)
	nFront = UBound (mRow)
	If aQuery.sPokemon = &quot;Eevee&quot; Then
		If aQuery.nPlayerLevel &lt;&gt; 0 Then
			ReDim Preserve mRow (nFront + 6) As Variant
			mRow (nFront + 1) = &quot;CP as &quot; _
				&amp; aBaseStats.mEvolved (0)
			mRow (nFront + 2) = &quot;Powered-up as &quot; _
				&amp; aBaseStats.mEvolved (0)
			mRow (nFront + 3) = &quot;CP as &quot; _
				&amp; aBaseStats.mEvolved (1)
			mRow (nFront + 4) = &quot;Powered-up as &quot; _
				&amp; aBaseStats.mEvolved (1)
			mRow (nFront + 5) = &quot;CP as &quot; _
				&amp; aBaseStats.mEvolved (2)
			mRow (nFront + 6) = &quot;Powered-up as &quot; _
				&amp; aBaseStats.mEvolved (2)
		Else
			ReDim Preserve mRow (nFront + 3) As Variant
			mRow (nFront + 1) = &quot;CP as &quot; _
				&amp; aBaseStats.mEvolved (0)
			mRow (nFront + 2) = &quot;CP as &quot; _
				&amp; aBaseStats.mEvolved (1)
			mRow (nFront + 3) = &quot;CP as &quot; _
				&amp; aBaseStats.mEvolved (2)
		End If
	Else
		If nEvolved = 0 Then
			If aQuery.nPlayerLevel &lt;&gt; 0 Then
				ReDim Preserve mRow (nFront + 1) As Variant
				mRow (nFront + 1) = &quot;Powered-up&quot;
			End If
		Else
			If aQuery.nPlayerLevel &lt;&gt; 0 Then
				ReDim Preserve mRow (nFront + nEvolved + 1) As Variant
				For nJ = 0 To nEvolved - 1
					mRow (nFront + nJ + 1) = &quot;CP as &quot; _
						&amp; aBaseStats.mEvolved (nJ)
				Next nJ
				mRow (UBound (mRow)) = &quot;Powered-up as &quot; _
					&amp; aBaseStats.mEvolved (nEvolved - 1)
			Else
				ReDim Preserve mRow (nFront + nEvolved) As Variant
				For nJ = 0 To nEvolved - 1
					mRow (nFront + nJ + 1) = &quot;CP as &quot; _
						&amp; aBaseStats.mEvolved (nJ)
				Next nJ
			End If
		End If
	End If
	mData (0) = mRow
	
	For nI = 0 To UBound (maIVs)
		mRow = Array ( _
			&quot;&quot;, &quot;&quot;, &quot;&quot;, &quot;&quot;, &quot;&quot;, _
			maIVs (nI).fLevel, maIVs (nI).nAttack, _
			maIVs (nI).nDefense, maIVs (nI).nStamina, _
			maIVs (nI).nTotal / 45)
		If aQuery.sPokemon = &quot;Eevee&quot; Then
			If aQuery.nPlayerLevel &lt;&gt; 0 Then
				ReDim Preserve mRow (nFront + 6) As Variant
				mRow (nFront + 1) = maIVs (nI).maEvolved (0).nCP
				mRow (nFront + 2) = maIVs (nI).maEvolved (0).nMaxCP
				mRow (nFront + 3) = maIVs (nI).maEvolved (1).nCP
				mRow (nFront + 4) = maIVs (nI).maEvolved (1).nMaxCP
				mRow (nFront + 5) = maIVs (nI).maEvolved (2).nCP
				mRow (nFront + 6) = maIVs (nI).maEvolved (2).nMaxCP
			Else
				ReDim Preserve mRow (nFront + 3) As Variant
				mRow (nFront + 1) = maIVs (nI).maEvolved (0).nCP
				mRow (nFront + 2) = maIVs (nI).maEvolved (1).nCP
				mRow (nFront + 3) = maIVs (nI).maEvolved (2).nCP
			End If
		Else
			If nEvolved = 0 Then
				If aQuery.nPlayerLevel &lt;&gt; 0 Then
					ReDim Preserve mRow (nFront + 1) As Variant
					mRow (nFront + 1) = maIVs (nI).nMaxCP
				End If
			Else
				If aQuery.nPlayerLevel &lt;&gt; 0 Then
					ReDim Preserve mRow (nFront + nEvolved + 1) As Variant
					For nJ = 0 To nEvolved - 1
						mRow (nFront + nJ + 1) = maIVs (nI).maEvolved (nJ).nCP
					Next nJ
					mRow (UBound (mRow)) = maIVs (nI).maEvolved (nEvolved - 1).nMaxCP
				Else
					ReDim Preserve mRow (nFront + nEvolved) As Variant
					For nJ = 0 To nEvolved - 1
						mRow (nFront + nJ + 1) = maIVs (nI).maEvolved (nJ).nCP
					Next nJ
				End If
			End If
		End If
		mData (nI + 1) = mRow
	Next nI
	
	&apos; Fills the query information at the first row
	mData (1) (0) = aBaseStats.sNo
	mData (1) (1) = aQuery.sPokemon
	mData (1) (2) = aQuery.nCP
	mData (1) (3) = aQuery.nHP
	mData (1) (4) = aQuery.nStarDust
	
	oRange = oSheet.getCellRangeByPosition ( _
		0, 0, UBound (mData (0)), UBound (mData))
	oRange.setDataArray (mData)
	oRange.setPropertyValue (&quot;VertJustify&quot;, _
		com.sun.star.table.CellVertJustify.TOP)
	
	oRange = oSheet.getCellRangeByPosition ( _
		0, 1, 0, UBound (mData))
	oRange.merge (True)
	oRange = oSheet.getCellRangeByPosition ( _
		1, 1, 1, UBound (mData))
	oRange.merge (True)
	oRange = oSheet.getCellRangeByPosition ( _
		2, 1, 2, UBound (mData))
	oRange.merge (True)
	oRange = oSheet.getCellRangeByPosition ( _
		3, 1, 3, UBound (mData))
	oRange.merge (True)
	oRange = oSheet.getCellRangeByPosition ( _
		4, 1, 4, UBound (mData))
	oRange.merge (True)
	oRange = oSheet.getCellRangeByPosition ( _
		9, 1, 9, UBound (mData))
	oRange.setPropertyValue (&quot;NumberFormat&quot;, 10)
	
	If aQuery.sPokemon = &quot;Eevee&quot; Then
		oRange = oSheet.getCellRangeByPosition ( _
			10, 0, 15, 0)
	Else
		If nEvolved = 0 Then
			oRange = oSheet.getCellRangeByPosition ( _
				10, 0, 10, 0)
		Else
			oRange = oSheet.getCellRangeByPosition ( _
				10, 0, 10 + nEvolved + 1, 0)
		End If
	End If
	oRange.setPropertyValue (&quot;IsTextWrapped&quot;, True)
	
	oRows = oSheet.getRows
	oRows.getByIndex (0).setPropertyValue (&quot;Height&quot;, 840)
	oColumns = oSheet.getColumns
	oColumns.getByIndex (0).setPropertyValue (&quot;Width&quot;, 890)
	oColumns.getByIndex (1).setPropertyValue (&quot;Width&quot;, 2310)
	oColumns.getByIndex (2).setPropertyValue (&quot;Width&quot;, 890)
	oColumns.getByIndex (3).setPropertyValue (&quot;Width&quot;, 890)
	oColumns.getByIndex (4).setPropertyValue (&quot;Width&quot;, 1780)
	oColumns.getByIndex (5).setPropertyValue (&quot;Width&quot;, 860)
	oColumns.getByIndex (6).setPropertyValue (&quot;Width&quot;, 860)
	oColumns.getByIndex (7).setPropertyValue (&quot;Width&quot;, 860)
	oColumns.getByIndex (8).setPropertyValue (&quot;Width&quot;, 860)
	oColumns.getByIndex (9).setPropertyValue (&quot;Width&quot;, 1030)
	If aQuery.sPokemon = &quot;Eevee&quot; Then
		If aQuery.nPlayerLevel &lt;&gt; 0 Then
			For nI = 0 To 5 Step 2
				oColumns.getByIndex (10 + nI).setPropertyValue ( _
					&quot;Width&quot;, 2310)
				oColumns.getByIndex (10 + nI + 1).setPropertyValue ( _
					&quot;Width&quot;, 2810)
			Next nI
		Else
			For nI = 0 To 2
				oColumns.getByIndex (10 + nI).setPropertyValue ( _
					&quot;Width&quot;, 2310)
			Next nI
		End If
	Else
		If nEvolved = 0 Then
			If aQuery.nPlayerLevel &lt;&gt; 0 Then
				oColumns.getByIndex (10).setPropertyValue ( _
					&quot;Width&quot;, 2200)
			End If
		Else
			For nI = 0 To nEvolved - 1
				oColumns.getByIndex (10 + nI).setPropertyValue ( _
					&quot;Width&quot;, 2310)
			Next nI
			If aQuery.nPlayerLevel &lt;&gt; 0 Then
				oColumns.getByIndex ( _
					10 + nEvolved).setPropertyValue ( _
					&quot;Width&quot;, 2810)
			End If
		End If
	End If
End Sub

&apos; fnFilterAppraisals: Filters the IV by the appraisals.
Function fnFilterAppraisals (aQuery As aFindIVParam, _
		nAttack As Integer, nDefense As Integer, _
		nStamina As Integer) As Boolean
	Dim nTotal As Integer, nMax As Integer, sBest As String
	
	&apos; The stats total.
	nTotal = nAttack + nDefense + nStamina
	If aQuery.nTotal = 1 And Not (nTotal &gt;= 37) Then
		fnFilterAppraisals = True
		Exit Function
	End If
	If aQuery.nTotal = 2 And Not (nTotal &gt;= 30 And nTotal &lt;= 36) Then
		fnFilterAppraisals = True
		Exit Function
	End If
	If aQuery.nTotal = 3 And Not (nTotal &gt;= 23 And nTotal &lt;= 29) Then
		fnFilterAppraisals = True
		Exit Function
	End If
	If aQuery.nTotal = 4 And Not (nTotal &lt;= 22) Then
		fnFilterAppraisals = True
		Exit Function
	End If
	&apos; The best stats.
	nMax = nAttack
	If nDefense &gt; nMax Then
		nMax = nDefense
	End If
	If nStamina &gt; nMax Then
		nMax = nStamina
	End If
	If aQuery.sBest &lt;&gt; &quot;&quot; Then
		sBest = &quot;&quot;
		If nAttack = nMax Then
			sBest = sBest &amp; &quot;Atk &quot;
		End If
		If nDefense = nMax Then
			sBest = sBest &amp; &quot;Def &quot;
		End If
		If nStamina = nMax Then
			sBest = sBest &amp; &quot;Sta &quot;
		End If
		If aQuery.sBest &lt;&gt; sBest Then
			fnFilterAppraisals = True
			Exit Function
		End If
	End If
	&apos; The max stat value.
	If aQuery.nMax = 1 And Not (nMax = 15) Then
		fnFilterAppraisals = True
		Exit Function
	End If
	If aQuery.nMax = 2 And Not (nMax = 13 Or nMax = 14) Then
		fnFilterAppraisals = True
		Exit Function
	End If
	If aQuery.nMax = 3 And Not (nMax &gt;= 8 And nMax &lt;= 12) Then
		fnFilterAppraisals = True
		Exit Function
	End If
	If aQuery.nMax = 4 And Not (nMax &lt;= 7) Then
		fnFilterAppraisals = True
		Exit Function
	End If
	fnFilterAppraisals = False
End Function

&apos; fnCalcCP: Calculates the combat power of the Pokémon
Function fnCalcCP (aBaseStats As aStats, fLevel As Double, _
		nAttack As Integer, nDefense As Integer, _
		nStamina As Integer) As Integer
	Dim nCP As Integer
		
	nCP = fnFloor ((aBaseStats.nAttack + nAttack) _
		* ((aBaseStats.nDefense + nDefense) ^ 0.5) _
		* ((aBaseStats.nStamina + nStamina) ^ 0.5) _
		* (fnGetCPM (fLevel) ^ 2) / 10)
	If nCP &lt; 10 Then
		nCP = 10
	End If
	fnCalcCP = nCP
End Function

&apos; fnCalcHP: Calculates the hit points of the Pokémon
Function fnCalcHP (aBaseStats As aStats, _
		fLevel As Double, nStamina As Integer) As Integer
	Dim nHP As Integer
	
	nHP = fnFloor ((aBaseStats.nStamina + nStamina) _
		* fnGetCPM (fLevel))
	If nHP &lt; 10 Then
		nHP = 10
	End If
	fnCalcHP = nHP
End Function

&apos; fnGetBaseStats: Returns the base stats of the Pokémon.
Function fnGetBaseStats (sPokemon As String) As aStats
	Dim nI As Integer
	
	subReadBaseStats
	For nI = 0 To UBound (maBaseStats)
		If maBaseStats (nI).sPokemon = sPokemon Then
			fnGetBaseStats = maBaseStats (nI)
			Exit Function
		End If
	Next nI
End Function

&apos; fnGetCPM: Returns the combat power multiplier.
Function fnGetCPM (fLevel As Double) As Double
	Dim nI As Integer
	
	subReadCPM
	If CInt (fLevel) = fLevel Then
		fnGetCPM = mCPM (fLevel)
	Else
		fnGetCPM = ((mCpm (fLevel - 0.5) ^ 2 _
			+ mCpm (fLevel + 0.5) ^ 2) / 2) ^ 0.5
	End If
End Function

&apos; fnFloor: Returns the floor of the number
Function fnFloor (fNumber As Double) As Integer
	fnFloor = CInt (fNumber - 0.5)
End Function

&apos; subReadBaseStats: Reads the base stats table.
Sub subReadBaseStats
	Dim mData As Variant, nI As Integer, nJ As Integer
	DIm nEvolved As Integer
	
	If UBound (maBaseStats) = -1 Then
		mData = fnGetBaseStatsData
		ReDim Preserve maBaseStats (UBound (mData)) As New aStats
		For nI = 0 To UBound (mData)
			With maBaseStats (nI)
				.sNo = mData (nI) (1)
				.sPokemon = mData (nI) (0)
				.nStamina = mData (nI) (2)
				.nAttack = mData (nI) (3)
				.nDefense = mData (nI) (4)
			End With
			nEvolved = UBound (mData (nI) (5))
			maBaseStats (nI).mEvolved = fnGetStringArray (nEvolved)
			For nJ = 0 To nEvolved
				maBaseStats (nI).mEvolved (nJ) = mData (nI) (5) (nJ)
			Next nJ
		Next nI
	End If
End Sub

&apos; fnGetStringArray: Obtains a blank string array
Function fnGetStringArray (nUBound As Integer) As Variant
	Dim mData () As String
	
	If nUBound &gt;= 0 Then
		ReDim Preserve mData (nUBound) As String
	End If
	fnGetStringArray = mData
End Function

&apos; fnGetEvolvedArray: Obtains a blank aEvolvedStats array
Function fnGetEvolvedArray (nUBound As Integer) As Variant
	Dim mData () As New aEvolvedStats
	
	If nUBound &gt;= 0 Then
		ReDim Preserve mData (nUBound) As New aEvolvedStats
	End If
	fnGetEvolvedArray = mData
End Function

&apos; fnReplace: Replaces all occurrances of a term to another.
Function fnReplace ( _
		sText As String, sFrom As String, sTo As String) As String
	Dim nPos As Integer
	
	nPos = InStr (sText, sFrom)
	Do While nPos &lt;&gt; 0
		sText = Left (sText, nPos - 1) &amp; sTo _
			&amp; Right (sText, Len (sText) - nPos - Len (sFrom) + 1)
		nPos = InStr (nPos + Len (sTo), sText, sFrom)
	Loop
	fnReplace = sText
End Function

&apos; subReadCPM: Reads the CPM table.
Sub subReadCPM
	If UBound (mCPM) = -1 Then
		mCPM = fnGetCPMData
	End If
End Sub

&apos; subReadStarDust: Reads the star dust table.
Sub subReadStarDust
	If UBound (mStarDust) = -1 Then
		mStarDust = fnGetStarDustData
	End If
End Sub
</script:module>